#!/usr/bin/env zsh

NOTES_DIR=${NOTES_DIR:-~/.notes}

new-note() {
  context="$1"
  if [ -z "$context" ]; then
    echo "context expected" >&2
    return 1
  fi

  datestamp=$(date +"%F")
  tempfile="$(mktemp -t note).md"
  $EDITOR "$tempfile"
  mkdir -p "$NOTES_DIR/$context"
  cat "$tempfile" >> "$NOTES_DIR/$context/$datestamp.md"
  echo >> "$NOTES_DIR/$context/$datestamp.md"
}

find-note() {
  pattern="$1"
  if [ -z "$pattern" ]; then
    echo "pattern expected" >&2
    return 1
  fi

  note=$(ag --nogroup "$pattern" "$NOTES_DIR" | sed "s|$NOTES_DIR||" | selecta)
  note_file=$(echo "$note" | cut -d : -f 1)
  note_file_number=$(echo "$note" | cut -d : -f 2)
  $EDITOR "+$note_file_number" "$NOTES_DIR/$note_file"
}

this-week() {
  context="$1"
  if [ -z "$context" ]; then
    echo "context expected" >&2
    return 1
  fi

  days=(sunday monday tuesday wednesday thursday friday saturday)
  tempfile="$(mktemp -t note).md"

  for day in $days; do
    last_week_day=$(date -v "$day" +"%F")
    file="$NOTES_DIR/$context/$last_week_day.md"
    if [[ -a $file ]]; then
      echo "# $last_week_day\n" >> "$tempfile"
      cat "$file" >> "$tempfile"
    fi
  done

  less -ci "$tempfile"
}

last-week() {
  context="$1"
  if [ -z "$context" ]; then
    echo "context expected" >&2
    return 1
  fi

  days=(sunday monday tuesday wednesday thursday friday saturday)
  tempfile="$(mktemp -t note).md"

  for day in $days; do
    last_week_day=$(date -v "$day" -v -7d +"%F")
    file="$NOTES_DIR/$context/$last_week_day.md"
    if [[ -a $file ]]; then
      echo "# $last_week_day\n" >> "$tempfile"
      cat "$file" >> "$tempfile"
    fi
  done

  less -ci "$tempfile"
}

edit() {
  context="$1"
  if [ -z "$context" ]; then
    echo "context expected" >&2
    return 1
  fi

  latest=$(ls "$NOTES_DIR/$context/"*.md | tail -1)

  if [[ -a $latest ]]; then
    $EDITOR $latest
  else
    return 1
  fi
}

note() {
  case "$1" in
    ("edit")
      edit "${@:2}"
      ;;
    ("new")
      new-note "${@:2}"
      ;;
    ("find")
      find-note "${@:2}"
      ;;
    ("last-week")
      last-week "${@:2}"
      ;;
    ("this-week")
      this-week "${@:2}"
      ;;
    (*)
      echo "command expected" >&2
      return 1
      ;;
  esac
}

_note-context-completion() {
  completions="$(ls "$NOTES_DIR")"
  reply=("${(ps:\n:)completions}")
}

compctl -k '(edit find last-week new this-week)' \
  -x 'c[-1,find]' -k '()' \
  - 'c[-1,edit]' -K _note-context-completion \
  - 'c[-1,new]' -K _note-context-completion \
  - 'c[-1,last-week]' -K _note-context-completion \
  - 'c[-1,this-week]' -K _note-context-completion \
  -- note
